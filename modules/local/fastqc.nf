process FASTQC {
    tag "${meta.id}"
    label 'process_medium'

    conda 'bioconda::fastqc=0.12.1'
    container "${params.fastqc_container ?: 'biocontainers/fastqc:0.12.1--hdfd78af_0'}"

    input:
    tuple val(meta), path(reads)

    output:
    tuple val(meta), path("*.html"), emit: html
    tuple val(meta), path("*.zip"),  emit: zip
    path "versions.yml",             emit: versions

    script:
    def args = task.ext.args ?: ''
    """
    fastqc \\
        ${args} \\
        --threads ${task.cpus} \\
        ${reads}

    cat <<-END_VERSIONS > versions.yml
    "${task.process}":
        fastqc: \$(fastqc --version | sed -n 's/FastQC v//p')
    END_VERSIONS
    """
}
